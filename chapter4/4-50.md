```c
#include <stdio.h>
long switchv(long idx) {
    long result = 0;
    switch(idx) {
        case 0:
            result = 0xaaa;
            break;
        case 2:
        case 5:
            result = 0xbbb;
            break;
        case 3:
            result = 0xccc;
            break;
        default:
            result = 0xddd;
    }
    return result;
}

#define CNT 8
#define MINVAL -1

int main() {
    long vals[CNT];
    long i;
    for (i = 0; i < CNT; i++) {
        vals[i] = switchv(i + MINVAL);
        printf("idx = %ld, val = 0x%lx\n", i + MINVAL, vals[i]);
    }
    return 0;
}
```
y86-64代码如下：
PS：手写汇编代码的感觉真是无法描述
```assembly
# Execution begins at address 0 
    .pos 0
    irmovq stack, %rsp      # Set up stack pointer
    call main       # Execute main program
    halt            # Terminate program 

# jump table
    .align 8
jt:
    .quad loc_A
    .quad loc_def
    .quad loc_B
    .quad loc_C
    .quad loc_def
    .quad loc_B
    .quad loc_def

# Array of 8 elements
    .align 8
array:  
    .quad 0x8
    .quad 0x7
    .quad 0x6
    .quad 0x5
    .quad 0x4
    .quad 0x2
    .quad 0x3
    .quad 0x1

main:   
    xorq %rsi, %rsi      # i = 0
    irmovq array, %rdx

loop:   
    irmovq $8,%r8        # Constant 8
    irmovq $1,%r9        # Constant 1
    
    rrmovq %rsi, %rdi
    subq %r9, %rdi      
    call switchv         # switchv(idx)
    rmmovq %rax,(%rdx)  
    addq %r8,%rdx        # start++
    addq %r9,%rsi        # i++
    rrmovq %rsi,%rcx
    subq %r8, %rcx      
    jl    loop           # i < 8
    ret

# long switchv(idx)
# idx in %rdi
switchv:
    rrmovq %rdi, %r10
    xorq %r11, %r11     
    subq %r11, %r10     # idx < 0
    jl loc_def
    rrmovq %rdi, %r10                    
    irmovq $5, %r12
    subq %r12, %r10     # idx > 5
    jg loc_def
    addq %rdi, %rdi     # 2*idx
    addq %rdi, %rdi     # 4*idx
    addq %rdi, %rdi     # 8*idx
    irmovq jt, %r13
    addq %rdi, %r13
    mrmovq (%r13), %r14
    pushq %r14
    ret
    
loc_A:
    irmovq $0xaaa,%rax
    jmp done
loc_B:
    irmovq $0xbbb,%rax
    jmp done
loc_C:
    irmovq $0xccc,%rax
    jmp done
loc_def:
    irmovq $0xddd,%rax
done:
    ret                  # Return

# Stack starts here and grows to lower addresses
    .pos 0x200
stack: 
```